plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.1'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.conchclub'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

jar {
	enabled = false
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'

    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.register('buildAndPushBE', Exec) {
	group = 'deployment'
	description = 'Build and Push Backend Image'
	commandLine 'cmd', '/c', 'docker build -t us-central1-docker.pkg.dev/conchclub/conchclub-repo/backend:latest -f docker/Dockerfile.backend . && docker push us-central1-docker.pkg.dev/conchclub/conchclub-repo/backend:latest'
}

tasks.register('buildAndPushFE', Exec) {
	group = 'deployment'
	description = 'Build and Push Frontend Image'
	commandLine 'cmd', '/c', 'docker build -t us-central1-docker.pkg.dev/conchclub/conchclub-repo/frontend:latest -f docker/Dockerfile.frontend ./client && docker push us-central1-docker.pkg.dev/conchclub/conchclub-repo/frontend:latest'
}

tasks.register('deployBE', Exec) {
	group = 'deployment'
	description = 'Deploy Backend to Cloud Run'
	commandLine 'cmd', '/c', 'gcloud', 'run', 'deploy', 'conchclub-backend',
		'--image', 'us-central1-docker.pkg.dev/conchclub/conchclub-repo/backend:latest',
		'--region', 'us-central1',
		'--allow-unauthenticated',
		'--port', '8080',
		'--service-account', 'conchclub-runner@conchclub.iam.gserviceaccount.com',
		'--set-secrets=SPRING_DATA_MONGODB_URI=mongodb-uri:latest,JWT_SECRET=jwt-secret:latest,TMDB_API_KEY=tmdb-key:latest,INVITE_CODE=invite-code:latest',
		'--set-env-vars=SPRING_PROFILES_ACTIVE=prod'
}

tasks.register('deployFE', Exec) {
	group = 'deployment'
	description = 'Deploy Frontend to Cloud Run'
	commandLine 'cmd', '/c', 'gcloud', 'run', 'deploy', 'conchclub-frontend',
		'--image', 'us-central1-docker.pkg.dev/conchclub/conchclub-repo/frontend:latest',
		'--region', 'us-central1',
		'--allow-unauthenticated',
		'--port', '80'
}

